import org.joda.time.DateTime

rule "daylight_colour_temp"
when
    // recompute every 60 seconds
    Time cron "42 * * ? * * *"
then
   var sunrise = new DateTime(Sunrise_Time.state.toString)
   var sunset = new DateTime(Sunset_Time.state.toString)
   // things should be cold around wakeup, neutral during the day, and warm during the night
   var preSunrise = sunrise.minusHours(1)
   var postSunrise = sunrise.plusHours(2)
   var preSunset = sunset.minusHours(1)

   var now = new DateTime()

   var w = 0.8
   if (now.isBefore(preSunrise)) {
       // 00:00 to pre-sunrise: 0.8
       w = 0.8
   } else if (now.isBefore(sunrise)) {
       // pre-sunrise to sunrise: 0.8 -> 0.3
       var dt = 1.0*(sunrise.millis - preSunrise.millis)
       var p = now.millis - preSunrise.millis
       w = 0.8 - (p/dt)*0.5
   } else if (now.isBefore(postSunrise)) {
       // sunrise to post-sunrise: 0.3 -> 0.5
       var dt = 1.0*(postSunrise.millis - sunrise.millis)
       var p = now.millis - sunrise.millis
       w = 0.3 + (p/dt)*0.2
   } else if (now.isBefore(preSunset)) {
       // post-sunrise to pre-sunset (day): 0.5 (neutral)
       w = 0.5
   } else if (now.isBefore(sunset)) {
       // pre-sunset to sunset: 0.5 -> 0.8
       var dt = 1.0*(sunset.millis - preSunset.millis)
       var p = now.millis - preSunset.millis
       w = 0.5 + (p/dt)*0.3
   } else {
       // after sunset: 0.8
       w = 0.8
   }

   logInfo("daylight", now.toString + " @ [" + Sunrise_Time.state + "," + Sunset_Time.state + "] => warmth: " + (w*100) + "%")
   DaylightCT.sendCommand(w * 100)
   if ((Flur_Gaestezimmer_Color.state as PercentType).intValue > 0) Flur_Gaestezimmer_ColorTemperature.sendCommand(w*100)
   if ((Flur_Wohnzimmer_Color.state as PercentType).intValue > 0)   Flur_Wohnzimmer_ColorTemperature.sendCommand(w*100)
end
